""
" @section Introduction, intro
" @library
" <doc/@plugin(name).txt> is generated by <https://github.com/google/vimdoc>.
" See <README.md> for more information about installation and screenshots.

""
" Append to |airline|'s section x.
"
" Default: ' %{nerdfont#find()} '`
call g:airline_renderer_nerdfont#utils#plugin.Flag('g:airline_renderer_nerdfont#x_suffix',
      \ ' %{nerdfont#find()} ')
""
" Override |airline|'s section y.
"
" Default: ' %{&fenc} %{airline_renderer_nerdfont#fileformat()} '`
call g:airline_renderer_nerdfont#utils#plugin.Flag('g:airline_renderer_nerdfont#y',
      \ ' %{&fenc} %{airline_renderer_nerdfont#fileformat()} ')
""
" Expand '%s' to icon then append to |airline|'s tabline.
call g:airline_renderer_nerdfont#utils#plugin.Flag('g:airline_renderer_nerdfont#tabline',
      \ ' %s ')

""
" Main function to render |airline|'s section x and y.
function! airline_renderer_nerdfont#main(...) abort
  let w:airline_section_x = get(w:, 'airline_section_x',
        \ get(g:, 'airline_section_x', ''))
  let w:airline_section_x .= g:airline_renderer_nerdfont#x_suffix
  if airline#parts#ffenc() !=? ''
    let w:airline_section_y = g:airline_renderer_nerdfont#y
  endif
endfunction

""
" Use icon to display fileformat.
function! airline_renderer_nerdfont#fileformat() abort
  if &fileformat ==# 'dos'
    return nerdfont#platform#find('windows')
  endif
  if &fileformat ==# 'mac'
    return nerdfont#platform#find('macos')
  endif
  if &fileformat ==# 'unix'
    if has('unix')
      return nerdfont#platform#find()
    else
      return nerdfont#platform#find('linux')
    endif
  endif
endfunction
